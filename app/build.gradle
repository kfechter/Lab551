plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.kennethfechter.lab551'
    compileSdk 34

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    defaultConfig {
        applicationId "com.kennethfechter.lab551"
        minSdk 24
        targetSdk 34
        if(System.getenv("DRONE_BUILD_NUMBER") != null) {
            versionCode = (8 + System.getenv("DRONE_BUILD_NUMBER").toInteger())
            versionName = new Date().format('yyyyMMdd') + "-" + System.getenv("DRONE_COMMIT").substring(0, 8)
        } else {
            versionCode = 2
            versionName = new Date().format('yyyyMMdd') + "-DevBuild"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("pettracer-upload-keystore.jks")
            storePassword System.getenv("KEYSTORE_MASTER")
            keyAlias "pettracer-upload-key"
            keyPassword System.getenv("RELEASE_SIGNING")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
        animationsDisabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

project.afterEvaluate {
    if (project.hasProperty("assembleRelease")) {
        assembleRelease.dependsOn connectedAndroidTest
        assembleRelease.dependsOn test
    }
}

dependencies {
    implementation(platform("com.google.firebase:firebase-bom:32.2.2"))
    implementation("com.google.firebase:firebase-analytics-ktx")
    implementation("com.google.firebase:firebase-crashlytics-ktx") {
        exclude group: 'androidx.datastore', module: 'datastore-preferences'
    }
    implementation "androidx.datastore:datastore-preferences:1.1.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.2'
    androidTestImplementation "androidx.test:rules:1.5.0"
}